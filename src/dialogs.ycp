/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/sudo/dialogs.ycp
 * Package:	Configuration of sudo
 * Summary:	Dialogs definitions
 * Authors:	Bubli <kmachalkova@suse.cz>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "sudo";

import "Label";
import "Wizard";
import "Sudo";
import "DialogTree";

include "sudo/helps.ycp";
include "sudo/complex.ycp";


string sudo_caption = _("Sudo Configuration");

symbol HandleUserSpecs(string key, map event) {
	any ret = event["ID"]:nil;

	if (ret == "add_spec") {
		current_spec_idx = Sudo::GetUserSpecSize();
		return `add_spec;
	} else if (ret == "edit_spec") {
		current_spec_idx = (integer) UI::QueryWidget(`id("table_user_spec"), `CurrentItem);
		if (Sudo::SystemRulePopup( Sudo::GetUserSpecMember(current_spec_idx), false)) 
			return `edit_spec;
	} else if (ret == "delete_spec") {
		integer idx = (integer) UI::QueryWidget(`id("table_user_spec"), `CurrentItem);
		list <term> items = (list <term>) UI::QueryWidget(`id("table_user_spec"), `Items);

		if (Sudo::SystemRulePopup( Sudo::GetUserSpecMember(idx), true)) {
			Sudo::RemoveUserSpec(idx);
			UI::ChangeWidget(`id("table_user_spec"), `Items, filter(term tmp, items,{
				return tmp[0]:nil != `id(idx);
			} ));
			Sudo::SetModified();
		}
	}
}

void InitUserSpecs (string key) {
	list <term> items = [];
	list <map <string,any> > specs = Sudo::GetUserSpecs();
	integer idx = 0;

	foreach(map <string, any> wrk, specs, {
		string cmds = "";
		if (size(wrk["commands"]:[]) > 1) {
			 cmds = mergestring(wrk["commands"]:[],",");
		} else
			cmds = (wrk["commands"]:[])[0]:""; 
		items = add(items, `item(`id(idx), (string) wrk["user"]:"", (string) wrk["host"]:"", (string) wrk["run_as"]:"",(wrk["no_passwd"]:false)? _("Yes") : _("No"), cmds));
		idx = idx + 1;
	});

	UI::ChangeWidget(`id("table_user_spec"),`Items,items);
}

symbol HandleHostAliases(string key, map event) {
	any ret = event["ID"]:nil;

	if (ret == "add_host_alias") {
		current_alias_name = "";
		return `add_h_alias;
	} else if (ret == "edit_host_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_host_aliases"), `CurrentItem);
		return `edit_h_alias;
	} else if (ret == "delete_host_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_host_aliases"),`CurrentItem);
		list <term> items = (list <term>) UI::QueryWidget(`id("table_host_aliases"), `Items);
		boolean confirm_delete = true;

		if(Sudo::SearchUserSpec("host",current_alias_name)) {
			if (!Popup::ContinueCancel (sformat(_("Host alias %1 is being used in one of sudo rules.
Deleting it may result in an inconsistent sudo configuration file. Really delete it ?
"), current_alias_name)))
				confirm_delete = false;
		}

		if (confirm_delete) {
			Sudo::RemoveHostAlias(current_alias_name);
			UI::ChangeWidget(`id("table_host_aliases"), `Items, filter(term tmp, items, {
				return tmp[0]:nil != `id(current_alias_name);
			} ));
			Sudo::SetModified();
		}
	}

}

void InitHostAliases(string key) {
	list <term> items = [];
	map <string, list<string> > aliases = Sudo::GetHostAliases();

	foreach(string key, list<string> value, aliases, {
		items = add (items, `item(`id(key), key, mergestring(value,",")));
	});

	UI::ChangeWidget(`id("table_host_aliases"),`Items,items);
}

symbol HandleUserAliases(string key, map event) {
	any ret = event["ID"]:nil;

	if (ret == "add_user_alias") {
		/* No alias name set so far */
		current_alias_name = "";
		return `add_u_alias;
	} else if (ret == "edit_user_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_user_aliases"),`CurrentItem);
		return `edit_u_alias;
	} else if (ret == "delete_user_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_user_aliases"),`CurrentItem);
		list <term> items = (list <term>) UI::QueryWidget(`id("table_user_aliases"), `Items);
		boolean confirm_delete = true;

		if(Sudo::SearchUserSpec("user",current_alias_name)) {
			if ( !Popup::ContinueCancel (sformat(_("User alias %1 is being used in one of sudo rules .
Deleting it may result in an inconsistent sudo configuration file. Really delete it ?
"), current_alias_name))) 
				confirm_delete = false;
		}

		if (confirm_delete) {
			Sudo::RemoveUserAlias(current_alias_name);
			UI::ChangeWidget(`id("table_user_aliases"), `Items, filter(term tmp, items, {
				return tmp[0]:nil != `id(current_alias_name);
			}));
			Sudo::SetModified();
		}
	}
}

void InitUserAliases (string key) {
	list <term> items = [];
	map <string, list<string> > aliases = Sudo::GetUserAliases();

	foreach(string key, list<string> value, aliases, {
		items = add (items, `item(`id(key), key, mergestring(value,",")));
	});

	UI::ChangeWidget(`id("table_user_aliases"),`Items,items);
}

symbol HandleRunAsAliases(string key, map event) {
	any ret = event["ID"]:nil;

	if (ret == "add_runas_alias") {
		/* No alias name set so far */
		current_alias_name = "";
		return `add_r_alias;
	} else if (ret == "edit_runas_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_runas_aliases"),`CurrentItem);
		return `edit_r_alias;
	} else if (ret == "delete_runas_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_runas_aliases"),`CurrentItem);
		list <term> items = (list <term>) UI::QueryWidget(`id("table_runas_aliases"), `Items);
		boolean confirm_delete = true;

		if(Sudo::SearchUserSpec("run_as","(" + current_alias_name + ")")) {
			if (!Popup::ContinueCancel (sformat(_("RunAs alias %1 is being used in one of sudo rules.
Deleting it may result in an inconsistent sudo configuration file. Really delete it ?
"), current_alias_name)))
				confirm_delete = false;
		}

		if (confirm_delete) {
			Sudo::RemoveRunAsAlias(current_alias_name);
			UI::ChangeWidget(`id("table_runas_aliases"), `Items, filter(term tmp, items, {
				return tmp[0]:nil != `id(current_alias_name);
			} ));
			Sudo::SetModified();
		}
	}

}

void InitRunAsAliases (string key) {
	list <term> items = [];
	map <string, list<string> > aliases = Sudo::GetRunAsAliases();

	foreach(string key, list<string> value, aliases, {
		items = add (items, `item(`id(key), key, mergestring(value,",")));
	});

	UI::ChangeWidget(`id("table_runas_aliases"),`Items,items);

}

symbol HandleCmndAliases(string key, map event) {
	any ret = event["ID"]:nil;

	if (ret == "add_command_alias") {
		/* No alias name set so far */
		current_alias_name = "";
		return `add_c_alias;
	} else if (ret == "edit_command_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_command_aliases"),`CurrentItem);
		return `edit_c_alias;
	} else if (ret == "delete_command_alias") {
		current_alias_name = (string) UI::QueryWidget(`id("table_command_aliases"),`CurrentItem);
		list <term> items = (list <term>) UI::QueryWidget(`id("table_command_aliases"), `Items);
		boolean confirm_delete = true;

		if(Sudo::SearchUserSpec("commands",current_alias_name)) {
			if (!Popup::ContinueCancel (sformat(_("Command alias %1 is being used in one of sudo rules.
Deleting it may result in an inconsistent sudo configuration file. Really delete it ?
"), current_alias_name)))
				confirm_delete = false;
		}

		if (confirm_delete) {
			Sudo::RemoveCmndAlias(current_alias_name);
			UI::ChangeWidget(`id("table_command_aliases"), `Items, filter(term tmp, items, {
				return tmp[0]:nil != `id(current_alias_name);
			} ));
			Sudo::SetModified();
		}
	}

}

void InitCmndAliases (string key) {
	list <term> items = [];
	map <string, list<string> > aliases = Sudo::GetCmndAliases();

	foreach(string key, list<string> value, aliases, {
		items = add (items, `item(`id(key), key, mergestring(value,",")));
	});

	UI::ChangeWidget(`id("table_command_aliases"),`Items,items);

}

map <string, map <string,any > > widgets_handling = $[
	"UserSpecifications" : $[
		"widget" : `custom,
		"custom_widget" : `VBox(),
		"init" : InitUserSpecs,
		"handle" : HandleUserSpecs,
		"help" : HELPS["spec"]:"",
	],
	"UserAliases" : $[
		"widget" : `custom,
		"custom_widget" : `VBox(),
		"init" : InitUserAliases,
		"handle" : HandleUserAliases,
		"help" : HELPS["user_aliases"]:"",
	],
	"RunAsAliases" : $[
		"widget" : `custom,
		"custom_widget" : `VBox(),
		"init" : InitRunAsAliases,
		"handle" : HandleRunAsAliases,
		"help" : HELPS["runas_aliases"]:"",
	],
	"HostAliases" : $[
		"widget" : `custom,
		"custom_widget" : `VBox(),
		"init" : InitHostAliases,
		"handle" : HandleHostAliases,
		"help" : HELPS["host_aliases"]:"",
	],
	"CommandAliases" : $[
		"widget" : `custom,
		"custom_widget" : `VBox(),
		"init" : InitCmndAliases,
		"handle" : HandleCmndAliases,
		"help" : HELPS["command_aliases"]:"",
	],
];

map<symbol,any> functions = $[
	`abort : Sudo::Abort,
];

map <string, map <string,any> > tabs = $[
	"user_specs" : $[
		"contents" : `VBox(
						`Table(
							`id("table_user_spec"),
							`header (
								_("Users"),
								_("Hosts"),
								_("RunAs"),
								_("NOPASSWD"),
								_("Commands")
							), []
						),
						`HBox (
							`PushButton(`id("add_spec"), `opt(`key_F3), " " + Label::AddButton() + " "),
							`PushButton(`id("edit_spec"), `opt(`key_F4), " " + Label::EditButton() + " "),
							`PushButton(`id("delete_spec"),`opt(`key_F5), " " + Label::DeleteButton() + " ")
						)
					),
		"caption" : sudo_caption + ": " + _("Rules for sudo"),
		"tree_item_label" : _("Rules for sudo "),
		"widget_names" : ["UserSpecifications"],
	],
	"user_aliases" : $[
		"contents" : `VBox(
						`Table(
							`id("table_user_aliases"),
							`header (
								_("Alias Name"),
								_("Members")
							), []
						),
						`HBox (
							`PushButton(`id("add_user_alias"), `opt(`key_F3), " " + Label::AddButton() + " "),
							`PushButton(`id("edit_user_alias"), `opt(`key_F4), " " + Label::EditButton() + " "),
							`PushButton(`id("delete_user_alias"), `opt(`key_F5), " " + Label::DeleteButton() + " ")
						)

					),
		"caption" : sudo_caption + ": " + _("User Aliases"),
		"tree_item_label" : _("User Aliases"),
		"widget_names" : ["UserAliases"],
	 ] ,
	"runas_aliases" : $[
		"contents" : `VBox(
						`Table(
							`id("table_runas_aliases"),
							`header (
								_("Alias Name"),
								_("Members")
							), []
						),
						`HBox (
							`PushButton(`id("add_runas_alias"), `opt(`key_F3), " " + Label::AddButton() + " "),
							`PushButton(`id("edit_runas_alias"), `opt(`key_F4), " " + Label::EditButton() + " "),
							`PushButton(`id("delete_runas_alias"), `opt(`key_F5), " " + Label::DeleteButton() + " ")
						)

					),
		"caption" : sudo_caption + ": " + _("RunAs Aliases"),
		"tree_item_label" : _("RunAs Aliases"),
		"widget_names" : ["RunAsAliases"],
	 ],
	"host_aliases" : $[
		"contents" : `VBox(
						`Table(
							`id("table_host_aliases"),
							`header (
								_("Alias Name"),
								_("Hosts")
							), []
						),
						`HBox (
							`PushButton(`id("add_host_alias"), `opt(`key_F3), " " + Label::AddButton() + " "),
							`PushButton(`id("edit_host_alias"), `opt(`key_F4), " " + Label::EditButton() + " "),
							`PushButton(`id("delete_host_alias"), `opt(`key_F5), " " + Label::DeleteButton() + " ")
						)

					),
		"caption" : sudo_caption + ": " + _("Host Aliases"),
		"tree_item_label" : _("Host Aliases"),
		"widget_names" : ["HostAliases"],
	 ],
	"cmnd_aliases" : $[
		"contents" : `VBox(
						`Table(
							`id("table_command_aliases"),
							`header (
								_("Alias Name"),
								_("Commands")
							), []
						),
						`HBox (
							`PushButton(`id("add_command_alias"), `opt(`key_F3), " " + Label::AddButton() + " "),
							`PushButton(`id("edit_command_alias"), `opt(`key_F4), " " + Label::EditButton() + " "),
							`PushButton(`id("delete_command_alias"), `opt(`key_F5), " " + Label::DeleteButton() + " ")
						)

					),
		"caption" : sudo_caption + ": " + _("Command Aliases"),
		"tree_item_label" : _("Command Aliases"),
		"widget_names" : ["CommandAliases"],
	 ],
];

any RunSudoDialogs() {

	list <string> tree_dialogs = ["user_specs", "user_aliases", "runas_aliases", "host_aliases", "cmnd_aliases"];


	return DialogTree::ShowAndRun ($[
		"ids_order" : tree_dialogs,
		"initial_screen" : initial_screen,
		"screens" : tabs,
		"widget_descr" : widgets_handling,
		"back_button" : Label::BackButton(),
		"abort_button" : Label::AbortButton(),
		"next_button" : Label::FinishButton(),
		"functions" : functions,
	]);
}

}
